apply plugin: 'groovy'
apply plugin: 'net.corda.plugins.publish-utils'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile "com.typesafe:config:1.3.0"
}

repositories {
    mavenCentral()
}

bintray {
    user = System.getenv('CORDA_BINTRAY_USER')
    key = System.getenv('CORDA_BINTRAY_KEY')
    publications = ['cordformation']
    dryRun = false
    pkg {
        repo = 'corda'
        name = 'cordformation'
        userOrg = 'r3'
        licenses = ['Apache-2.0']

        version {
            gpg {
                sign = true
                passphrase = System.getenv('CORDA_BINTRAY_GPG_PASSPHRASE')
            }
        }
    }
}

publishing {
    publications {
        cordformation(MavenPublication) {
            from components.java
            groupId  'net.corda.plugins'
            artifactId 'cordformation'

            artifact sourceJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'cordformation'
                    description 'A small gradle plugin for adding some basic Quasar tasks and configurations to reduce build.gradle bloat.'
                    url 'https://github.com/corda/corda'
                    scm {
                        url 'https://github.com/corda/corda'
                    }

                    licenses {
                        license {
                            name 'Apache-2.0'
                            url 'https://www.apache.org/licenses/LICENSE-2.0'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'R3'
                            name 'R3'
                            email 'dev@corda.net'
                        }
                    }
                }
            }
        }
    }
}
