plugins {
    id 'de.sebastianboegl.shadow.transformer.log4j' version '2.1.1' apply false
}

description 'Database Migration Tool'

apply plugin: 'application'
// We need to set mainClassName before applying the shadow plugin.
mainClassName = 'com.r3.corda.dbmigration.DBMigration'

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.corda.plugins.publish-utils'
apply plugin: 'com.jfrog.artifactory'

dependencies {
    compile(project(':node')) {
        // The Node only needs this for binary compatibility with Cordapps written in Kotlin 1.1.
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jre8'
    }
    compile project(':tools:cliutils')
}

import de.sebastianboegl.gradle.plugins.shadow.transformers.Log4j2PluginsFileTransformer
shadowJar {
    transform(Log4j2PluginsFileTransformer)
    baseName = 'database-manager'
}

task buildMigrationTool(dependsOn: shadowJar)
assemble.dependsOn buildMigrationTool

artifacts {
    publish shadowJar {
        classifier = ''
    }
}

jar {
    classifier 'ignore'
    enabled = false
}

publish {
    disableDefaultJar = true
    name 'tools-database-manager'
}
